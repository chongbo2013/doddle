buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://plugins.gradle.org/m2/" }
        jcenter()
    }
    dependencies {
        classpath 'de.richsource.gradle.plugins:gwt-gradle-plugin:0.6'
        classpath 'com.android.tools.build:gradle:2.3.0'
        classpath 'org.multi-os-engine:moe-gradle:1.3.6'
        classpath 'net.ltgt.gradle:gradle-apt-plugin:0.9'
        classpath 'com.badlogicgames.gdx:gdx-tools:1.9.6'
    }
}

allprojects {
    apply plugin: "eclipse"
    apply plugin: "idea"

    version = '0.7.4'
    ext {
        appName = "dodles"
        gdxVersion = '1.9.6'
        roboVMVersion = '2.3.1'
        box2DLightsVersion = '1.4'
        ashleyVersion = '1.7.0'
        aiVersion = '1.8.0'
        daggerVersion = '2.0.2' // could be upgrade to 2.2? 2.6?
        visuiVersion = '1.3.0' // only needed for legacy VisUI implementation?
        czyzbyVersion = '1.8.1.9.4' // only needed for legacy VisUI implementation?
    }

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }
        maven { url "https://maven.google.com" }
        maven { url "$rootDir/node_modules/react-native/android" }
    }
}

project(":desktop") {
    apply plugin: "java"


    dependencies {
        compile project(":core")
        compile "com.badlogicgames.gdx:gdx-backend-lwjgl:$gdxVersion"
        compile "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
        compile "com.badlogicgames.gdx:gdx-box2d-platform:$gdxVersion:natives-desktop"
    }
}

project(":android") {
    apply plugin: "android"

    configurations { natives }

    dependencies {
        compile project(":core")
        compile "com.badlogicgames.gdx:gdx-backend-android:$gdxVersion"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-armeabi"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-armeabi-v7a"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-arm64-v8a"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-x86"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-x86_64"
        compile "com.badlogicgames.gdx:gdx-box2d:$gdxVersion"
        natives "com.badlogicgames.gdx:gdx-box2d-platform:$gdxVersion:natives-armeabi"
        natives "com.badlogicgames.gdx:gdx-box2d-platform:$gdxVersion:natives-armeabi-v7a"
        natives "com.badlogicgames.gdx:gdx-box2d-platform:$gdxVersion:natives-arm64-v8a"
        natives "com.badlogicgames.gdx:gdx-box2d-platform:$gdxVersion:natives-x86"
        natives "com.badlogicgames.gdx:gdx-box2d-platform:$gdxVersion:natives-x86_64"
        compile "com.facebook.react:react-native:0.45.1"
    }
}

project(":html") {
    apply plugin: "gwt"
    apply plugin: "war"


    dependencies {
        compile project(":core")
        compile "com.badlogicgames.gdx:gdx-backend-gwt:$gdxVersion"
        compile "com.badlogicgames.gdx:gdx:$gdxVersion:sources"
        compile "com.badlogicgames.gdx:gdx-backend-gwt:$gdxVersion:sources"
        compile "com.badlogicgames.gdx:gdx-box2d:$gdxVersion:sources"
        compile "com.badlogicgames.gdx:gdx-box2d-gwt:$gdxVersion:sources"
    }
}

project(":core") {
    apply plugin: "java"
    apply plugin: "net.ltgt.apt"

    dependencies {
        compile "com.badlogicgames.gdx:gdx:$gdxVersion"
        compile "com.badlogicgames.gdx:gdx-box2d:$gdxVersion"
        apt "com.google.dagger:dagger-compiler:$daggerVersion"
        compile "com.google.dagger:dagger:$daggerVersion"
        compile "com.kotcrab.vis:vis-ui:$visuiVersion" // only needed for legacy VisUI implementation?
        compile "com.github.czyzby:gdx-lml:$czyzbyVersion" // only needed for legacy VisUI implementation?
        compile "com.github.czyzby:gdx-lml-vis:$czyzbyVersion" // only needed for legacy VisUI implementation?
    }
}

project(":ios-moe") {
    apply plugin: "moe"

    configurations { natives }

    dependencies {
        compile project(":core")
        //compile "com.badlogicgames.gdx:gdx-backend-moe:$gdxVersion"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-ios"
        natives "com.badlogicgames.gdx:gdx-box2d-platform:$gdxVersion:natives-ios"
    }
}

tasks.eclipse.doLast {
    delete ".project"
}

// Texture Packing for Dodle Engine UI
project.ext.texturePackerAtlases = [
        // [ "android/assets/input/path/", "android/assets/output/path/", "atlas_name" ]
        ["resources/libgdx-source-images/ui", "android/assets/editor/ui", "ui"],
        ["resources/libgdx-source-images/demo", "android/assets/demo", "demo"]
]
import com.badlogic.gdx.tools.texturepacker.TexturePacker
task texturePacker << {
    if (project.ext.has('texturePackerAtlases')) {
        texturePackerAtlases.each {
            TexturePacker.process(it[0], it[1], it[2])
        }
    }
}